// Apply Gradle plugins
plugins {
	id 'java'
	id 'eclipse'
	id 'idea'

    id 'com.github.hierynomus.license' version '0.11.0'
	id 'com.github.johnrengelman.shadow' version '1.2.0'
}

apply plugin: 'checkstyle'

defaultTasks 'clean', 'licenseFormat', 'build', 'shadowJar'

// Project information
group = 'net.amigocraft.mpt'
version = '0.1.0-SNAPSHOT'

// Extended project information
ext.projectName = 'MPT'
ext.description = 'A Bukkit plugin similar to Debian\'s apt-get package, but with Minecraft maps instead of packages.'
ext.inceptionYear = '2014'
ext.packaging = 'jar'
ext.versionSuffix = version.contains('SNAPSHOT') && System.getenv("BUILD_NUMBER") ? '-jnks' + System.getenv("BUILD_NUMBER") : ''

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    bukkit = 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'
	jsonSimple = 'com.googlecode.json-simple:json-simple:1.1.1'
	updater = 'net.gravitydevelopment.updater:updater:2.3'
}

// Project repositories
repositories {
	mavenCentral()
	maven { url 'http://hub.spigotmc.org/nexus/content/groups/public/' }
	maven { url 'http://repo.gravitydevelopment.net/' }
}

// Project dependencies
dependencies {
    compile bukkit
	compile jsonSimple
	compile updater
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

processResources {
  filter { String line -> line.replace('%VERSION%', versionSuffix) }
}

jar { // The Pore classes will be created in Pore-VERSION-base.jar first
	classifier = 'base'
}

shadowJar {
	dependencies {
		include dependency(jsonSimple)
		include dependency(updater)
	}
	classifier = ''
}

// License header formatting
license {
	header file('LICENSE')
	exclude '*.txt'
	sourceSets = project.sourceSets
	ignoreFailures false
	strictCheck true
	mapping {
		java = 'SLASHSTAR_STYLE'
	}
}

// check code style
checkstyle {
    configProperties = [
            "name"        : project.name,
            "year"        : project.inceptionYear
    ]
    configFile = file("etc/checkstyle.xml")
}
